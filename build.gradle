import java.time.LocalDateTime
import java.time.Duration

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.7'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id "org.seasar.doma.compile" version '1.1.0'
	id "io.freefair.lombok" version "6.6.1"
	id "com.diffplug.spotless" version "6.12.0"
}

group = 'vss.jp.net'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

spotless {
	java {
		googleJavaFormat('1.6')
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-mysql'
	implementation 'com.mysql:mysql-connector-j:8.0.31'

	// for DOMA2
	implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.7.0'
	annotationProcessor 'org.seasar.doma:doma-processor:2.53.1'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.flywaydb.flyway-test-extensions:flyway-spring-test:7.0.0'
}

tasks.named('test') {
	useJUnitPlatform()

	var startTime
	doFirst {
		startTime = LocalDateTime.now()
		println("start test: " + startTime)
	}

	var endTime
	doLast {
		endTime = LocalDateTime.now()
		println("end test: " + endTime)

		var duration = Duration.between(startTime, endTime);
		println("duration: " + duration.toMillis() + "[ms]")
	}
}
